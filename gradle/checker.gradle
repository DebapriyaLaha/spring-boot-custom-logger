tasks.withType(Pmd) {
    ignoreFailures true
    reports {
        xml.enabled true
        html.enabled true
    }
}

tasks.withType(FindBugs) {
    ignoreFailures true
    effort 'max'
    reportLevel 'low'
    reports {
        xml.enabled true
        html.enabled false
    }
}

checkstyle { toolVersion '8.11' }

tasks.withType(Checkstyle) {
    ignoreFailures true
    showViolations false
    configFile 'gradle/checkstyle.xml' as File
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    jacoco { append false }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}